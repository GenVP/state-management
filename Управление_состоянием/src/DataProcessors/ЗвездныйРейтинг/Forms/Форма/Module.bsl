// https://gist.github.com/KalyakinAG/88588b2c3925f8a465851bce52dbab13
#Область УправлениеСостоянием

&НаСервере
Процедура ОбновитьФормуНаСервере(ИзмененныеРеквизиты = Неопределено, СохраненноеСостояние = Неопределено)
	РаботаСФормойКлиентСервер.ОбновитьФорму(ЭтотОбъект, ИзмененныеРеквизиты, СохраненноеСостояние);	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(ИзмененныеРеквизиты = Неопределено)
	Перем СохраненноеСостояние;
	Если НЕ РаботаСФормойКлиентСервер.ОбновитьФорму(ЭтотОбъект, ИзмененныеРеквизиты, СохраненноеСостояние) Тогда
		ОбновитьФормуНаСервере(ИзмененныеРеквизиты, СохраненноеСостояние);
	КонецЕсли;
КонецПроцедуры

#Область ОбработчикиСобытийФормы

//@skip-warning
&НаКлиенте
Процедура ПриАктивизацииСтроки(Элемент)
	РаботаСФормойКлиентСервер.ПриАктивизацииСтроки(ЭтотОбъект, Элемент);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	РаботаСФормойКлиентСервер.НачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

#Область ПриИзменении // При расчете может потребоваться изменить контекст, а сделать это можно только в модуле формы

//@skip-warning
//  Процедура продолжает расчет уже в контексте сервера. Такое разделение процедуры нужно для программного переключения контекста
&НаСервере
Процедура РассчитатьСостояниеНаСервере(ИзмененныеРеквизиты, СохраненноеСостояние)
	РаботаСМодельюКлиентСервер.РассчитатьСостояние(ЭтотОбъект, ИзмененныеРеквизиты, СохраненноеСостояние);
	ОбновитьФормуНаСервере(ИзмененныеРеквизиты);
КонецПроцедуры

//  Процедура выполняет расчет в контексте клиента
&НаКлиенте
Процедура РассчитатьСостояние(ИзмененныеРеквизиты)
	Перем СохраненноеСостояние;
	Если НЕ РаботаСМодельюКлиентСервер.РассчитатьСостояние(ЭтотОбъект, ИзмененныеРеквизиты, СохраненноеСостояние) Тогда
		РассчитатьСостояниеНаСервере(ИзмененныеРеквизиты, СохраненноеСостояние);
	Иначе
		ОбновитьФорму(ИзмененныеРеквизиты);
	КонецЕсли;
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура ПриИзменении(Элемент)
	Модель = РаботаСМодельюКлиентСервер.МодельОбъекта(ЭтотОбъект);
	Параметр = Модель.Параметры[Модель.ПараметрыЭлементов[Элемент.Имя]];
	Если Параметр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПроверятьЗначение = ТипЗнч(Элемент) = Тип("ПолеФормы")
		И Элемент.Вид = ВидПоляФормы.ПолеВвода
		И Элемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.Авто;
	
	Если Параметр.ЭтоЭлементКоллекции Тогда
		ИзмененныеРеквизиты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РаботаСМодельюКлиентСервер.Реквизит(Параметр.Идентификатор, 
			РаботаСМодельюКлиентСервер.ЗначениеПараметра(ЭтотОбъект, Модель, Модель.Параметры[Параметр.Коллекция + ".ИндексСтроки"],
			ПроверятьЗначение)));
	Иначе
		ИзмененныеРеквизиты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РаботаСМодельюКлиентСервер.Реквизит(Параметр.Идентификатор,, ПроверятьЗначение));
	КонецЕсли;
	РассчитатьСостояние(ИзмененныеРеквизиты);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//  Создание описания модели. Начало
	Модель = РаботаСМодельюКлиентСервер.Модель("МодельЗвездныйРейтинг", "Объект");
	//  Описание самой формы
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель,,, "Рейтинг");
	//  Закрашенные звезды
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда11,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда21,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда31,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда41,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда51,, "Рейтинг");
	//  Незакрашенные звезды
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда10,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда20,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда30,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда40,, "Рейтинг");
	РаботаСФормойКлиентСервер.Элемент(ЭтотОбъект, Модель, Элементы.ДекорацияЗвезда50,, "Рейтинг");
	//  Создание описания модели. Конец
	
	РаботаСМоделью.Инициализировать(ЭтотОбъект, Модель);
	
	//  Отображение состояния формы
	ОбновитьФормуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗвездаНажатие(Элемент)
	Перем ИзмененныеРеквизиты;
	//  Определение состояния звезды
	СоставИмениЭлементаЗвезды = СтрЗаменить(Элемент.Имя, "ДекорацияЗвезда", "");
	СостояниеЗвезды = Прав(СоставИмениЭлементаЗвезды, 1);
	РейтингЗвезды = Лев(СоставИмениЭлементаЗвезды, 1);
	//  Установка нового значения рейтинга по формуле: РейтингЗвезды - СостояниеЗвезды
	РаботаСМодельюКлиентСервер.УстановитьЗначение(ЭтотОбъект, "Рейтинг",, РейтингЗвезды - СостояниеЗвезды, ИзмененныеРеквизиты);
	//  Обновление формы 
	ОбновитьФорму(ИзмененныеРеквизиты);
	//  Возвращение фокуса на элемент звезды, которая была нажата
	ЭтотОбъект.ТекущийЭлемент = Элементы[СтрШаблон("ДекорацияЗвезда%1%2", РейтингЗвезды, ?(СостояниеЗвезды = "1", "0", "1"))];
КонецПроцедуры
